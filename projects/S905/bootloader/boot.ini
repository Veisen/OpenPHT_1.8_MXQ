#------------------------------------------------------------------------------------------------------
#
# boot.ini identification
#
#------------------------------------------------------------------------------------------------------
ODROIDC2-UBOOT-CONFIG

#------------------------------------------------------------------------------------------------------
#
# Boot Specific Stuff
#
#------------------------------------------------------------------------------------------------------
setenv bootrootfs "BOOT_IMAGE=kernel.img boot=LABEL=LIBREELEC disk=LABEL=LIBREELEC_DISK"

#------------------------------------------------------------------------------------------------------
#
# Console Setup
#
#------------------------------------------------------------------------------------------------------
setenv console "console=ttyS0,115200"

#------------------------------------------------------------------------------------------------------
#
# Video
#   Setup the video output (default is 1080p60hz)
#     480 Lines (720x480)
#       setenv hdmimode "480i60hz"       # Interlaced 60Hz
#       setenv hdmimode "480i_rpt"       # Interlaced for Rear Projection Televisions 60Hz
#       setenv hdmimode "480p60hz"       # 480 Progressive 60Hz
#       setenv hdmimode "480p_rpt"       # 480 Progressive for Rear Projection Televisions 60Hz
#     576 Lines (720x576)
#       setenv hdmimode "576i50hz"       # Interlaced 50Hz
#       setenv hdmimode "576i_rpt"       # Interlaced for Rear Projection Televisions 50Hz
#       setenv hdmimode "576p50hz"       # Progressive 50Hz
#       setenv hdmimode "576p_rpt"       # Progressive for Rear Projection Televisions 50Hz
#     720 Lines (1280x720)
#       setenv hdmimode "720p60hz"       # Progressive 60Hz
#       setenv hdmimode "720p50hz"       # Progressive 50Hz
#     1080 Lines (1920x1080)
#       setenv hdmimode "1080i60hz"      # Interlaced 60Hz
#       setenv hdmimode "1080p60hz"      # Progressive 60Hz
#       setenv hdmimode "1080i50hz"      # Interlaced 50Hz
#       setenv hdmimode "1080p50hz"      # Progressive 50Hz
#       setenv hdmimode "1080p30hz"      # Progressive 30Hz
#       setenv hdmimode "1080p25hz"      # Progressive 25Hz
#       setenv hdmimode "1080p24hz"      # Progressive 24Hz
#     4K (3840x2160)
#       setenv hdmimode "2160p60hz"      # Progressive 60Hz
#       setenv hdmimode "2160p50hz"      # Progressive 50Hz
#       setenv hdmimode "2160p30hz"      # Progressive 30Hz
#       setenv hdmimode "2160p25hz"      # Progressive 25Hz
#       setenv hdmimode "2160p24hz"      # Progressive 24Hz
#       setenv hdmimode "2160p60hz420"   # Progressive 60Hz with YCbCr 4:2:0 (Requires TV/Monitor that supports it)
#       setenv hdmimode "2160p50hz420"   # Progressive 50Hz with YCbCr 4:2:0 (Requires TV/Monitor that supports it)
#     4K2K (4096x2160)
#       setenv hdmimode "smpte60hz"      # Progressive 60Hz SMPTE
#       setenv hdmimode "smpte50hz"      # Progressive 50Hz SMPTE
#       setenv hdmimode "smpte30hz"      # Progressive 30Hz SMPTE
#       setenv hdmimode "smpte25hz"      # Progressive 25Hz SMPTE
#       setenv hdmimode "smpte24hz"      # Progressive 24Hz SMPTE
#       setenv hdmimode "smpte60hz420"   # Progressive 60Hz SMPTE with YCbCr 4:2:0 (Requires TV/Monitor that supports it)
#       setenv hdmimode "smpte50hz420"   # Progressive 50Hz SMPTE with YCbCr 4:2:0 (Requires TV/Monitor that supports it)
#
#------------------------------------------------------------------------------------------------------
setenv hdmimode "1080p60hz"

#------------------------------------------------------------------------------------------------------
#
# LibreELEC variables
#
# Setup the LibreELEC options
#   valid values are: textmode debugging progress nofsck nosplash noram overlay quiet ssh
#------------------------------------------------------------------------------------------------------
setenv libreelec ""

#------------------------------------------------------------------------------------------------------
#
# Amremote
#
#------------------------------------------------------------------------------------------------------
setenv amremote "0"

#------------------------------------------------------------------------------------------------------
#
# Enable|Disable HDMI CEC Control
#   setenv hdmi_cec  "0"                  # Disabled
#   setenv hdmi_cec  "1"                  # Enabled
#
#------------------------------------------------------------------------------------------------------
setenv hdmi_cec "1"

#------------------------------------------------------------------------------------------------------
#
# CPU Frequency / Cores control
#
# WARNING!!! WARNING!!! WARNING!!!
#   Before changing anything here please read the wiki entry:
#   http://odroid.com/dokuwiki/doku.php?id=en:c2_set_cpu_freq
#
# MAX CPU's
#   setenv maxcpus "1"
#   setenv maxcpus "2"
#   setenv maxcpus "3"
#   setenv maxcpus "4"
#
# MAX Frequency
#   setenv max_freq "2016"  # 2.016GHz
#   setenv max_freq "1944"  # 1.944GHz
#   setenv max_freq "1944"  # 1.944GHz
#   setenv max_freq "1920"  # 1.920GHz
#   setenv max_freq "1896"  # 1.896GHz
#   setenv max_freq "1752"  # 1.752GHz
#   setenv max_freq "1680"  # 1.680GHz
#   setenv max_freq "1656"  # 1.656GHz
#   setenv max_freq "1536"  # 1.536GHz
#
#------------------------------------------------------------------------------------------------------
setenv maxcpus "4"
setenv max_freq "1536"

#------------------------------------------------------------------------------------------------------
#
# Odroid C2 specific
#
#------------------------------------------------------------------------------------------------------
setenv odroidp1 "no_console_suspend logo=${logoopt} vout=${outputmode},enable hdmimode=${hdmimode} cvbsmode=nocvbs"
setenv odroidp2 "mac=${ethaddr} consoleblank=0 max_freq=${max_freq} maxcpus=${maxcpus}"
setenv odroid   "${odroidp1} ${odroidp2}"
if test "${hdmi_cec}" = "1"; then setenv cec "hdmitx=cec17"; fi

# Prepare to boot
setenv loadaddr "0x10800000"
fatload mmc 0:1 ${loadaddr} kernel.img

# Populate the final bootcmd
setenv bootargs "${console} ${bootrootfs} ${odroid} ${cec} ${libreelec}"

# Device tree modifications
bootm start ${loadaddr}
bootm loados
bootm fdt
if test "${amremote}" = "1"; then fdt set /meson-ir status disabled; fdt set /meson-remote status okay; fi
bootm prep

# Boot the board
bootm go
